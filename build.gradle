plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.0'
	id 'io.spring.dependency-management' version '1.1.4'

	/**
	 * AsciiDoctor
	 **/
	id "org.asciidoctor.jvm.convert" version "3.3.2"
}

group = 'cat.community'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	asciidoctorExt
}

repositories {
	mavenCentral()

	/* Vue build */
	gradlePluginPortal()
}

dependencies {
	/**
	 * Spring Framework
	 **/
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-tomcat'
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	implementation 'org.springframework.boot:spring-boot-starter-mustache'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

	/**
	 * scrypt
	 **/
	implementation 'org.springframework.security:spring-security-crypto'
	implementation 'org.bouncycastle:bcprov-jdk15on:1.70'

	/**
	 * File Upload
	 **/
	implementation group: 'commons-fileupload', name: 'commons-fileupload', version: '1.4'

	/**
	 * Lombok
	 **/
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	compileOnly 'javax.mail:mail:1.4.7'

	/**
	 * MYSQL
	 **/
	implementation 'mysql:mysql-connector-java:8.0.26'

	/**
	 * JPA, QUERYDSL
	 **/
	implementation 'com.querydsl:querydsl-core'
	implementation 'com.querydsl:querydsl-jpa'
	annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jpa"
	annotationProcessor 'jakarta.persistence:jakarta.persistence-api'
	annotationProcessor 'jakarta.annotation:jakarta.annotation-api'

	/**
	 * AsciiDOC - SpringRESTDOC
	 **/
	asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor:2.0.6.RELEASE'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc:2.0.6.RELEASE'

	/**
	 * JWT
	 **/
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

	/**
	 * GSON
	 **/
	implementation 'com.google.code.gson:gson:2.8.7'
}

ext {
	snippetsDir = file('build/generated-snippets')
}

test {
	useJUnitPlatform()
	scanForTestClasses = false
	include "**/*Test.class"
	outputs.dir snippetsDir
}

asciidoctor {
	inputs.dir snippetsDir
	configurations 'asciidoctorExt'
	dependsOn test
}

bootJar {
	enabled = true
	dependsOn asciidoctor

	copy {
		from asciidoctor.outputDir
		into "src/main/resources/templates/docs"
	}
}

jar {
	enabled = false
}